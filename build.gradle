plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.spotbugs" version "4.6.0"
}

group = 'gr.james'
version = '0.16'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

spotbugs {
    spotbugsTest.enabled = false
    effort = "max"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Simple Graph'
                description = 'Simple Graph is a graph interface for Java 6 that is designed to expose a very simple API to support working with graphs'
                url = 'https://github.com/gstamatelat/simple-graph'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'gstamatelat'
                        name = 'Giorgos Stamatelatos'
                        email = 'gstamatelat@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gstamatelat/simple-graph.git'
                    developerConnection = 'scm:git:git@github.com:gstamatelat/simple-graph.git'
                    url = 'https://github.com/gstamatelat/simple-graph'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("${buildDir}/publishing-repository")
        }
    }
}

wrapper {
    gradleVersion = '6.5.1'
}

javadoc {
    doLast {
        new File(destinationDir, 'stylesheet.css').append(file('src/javadoc/stylesheet.css').text)
    }
    options.header('simple-graph ' + version)
}

dependencies {
    testImplementation 'junit:junit:4.12'
}
